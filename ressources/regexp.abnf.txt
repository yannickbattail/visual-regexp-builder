regepliteral = "/" [ "^" ] regexpchoice [ "$" ] "/" [ "g" ] [ "i" ] [ "m" ]
regexpchoice = regexpsequence *( "|" regexpsequence )
regexpsequence = 1*( regexpfactor [ regexpquantifier ] )
regexpfactor = charfactor / regexpescape / regexpclass / regexpgroup
regexpescape = "\" (
     "f" / "n" / "r" / "t" /
     ( "u" HEXDIG HEXDIG HEXDIG HEXDIG) /
     "B" / "D" / "S" / "W" /
     "b" / "d" / "s" / "w" /
     charspecialfactor / DIGIT )
regexpgroup = "(" [ "?" [ ":" / "=" / "!" ] ] regexpchoice ")"
regexpclass = "[" [ "^" ] 1*( 1*( charclass / regexpclassescape ) [ "-" 1*( charclass / regexpclassescape ) ] ) "]"
regexpclassescape = "\" (
     "b" / "f" / "n" / "r" / "t" /
     ( "u" HEXDIG HEXDIG HEXDIG HEXDIG) /
     "D" / "S" / "W" /
     "d" / "s" / "w" /
     charspecial / DIGIT )
regexpquantifier = ( "?" / "*" / "+" / ( "{" integer [ "," [ integer ] ] "}" ) ) [ "?" ]

charfactor =  ALPHA / DIGIT / "#" / "%" / "&" / "!" / "'" / "," / "-" / ":" / ";" / "<" / "=" / ">" / "@" / "_" / "`" / "~" / "." ; not /\[]{}()?+*|.^$
charclass = ALPHA / DIGIT / "{" / "}" / "(" / ")" / "." / "$" / "+" / "*" / "?" / "|" / "#" / "%" / "&" / "!" / "'" / "," / ":" / ";" / "<" / "=" / ">" / "@" / "_" / "`" / "~" ; not /\[]^-
charspecialfactor =  "[" / "\" / "]" / "^" / "/" / "{" / "}" / "(" / ")" / "?" / "+" / "*" / "|" / "." /"^" / "$" / " "
charspecial =  "-" / "[" / "\" / "]" / "^" / " "

integer = 1*DIGIT

HEXDIG = DIGIT / "a" / "b" / "c" / "d" / "e" / "f" / "A" / "B" / "C" / "D" / "E" / "F"
DIGIT = %x30-39
ALPHA = %x41-5A / %x61-7A
